#pragma kernel CSMain
#include "SNoise.hlsl"
#define WIDTH 64
#define EPSILON 1e-3


static const float4 Dx = float4(EPSILON, 0, 0, 0);
static const float4 Dy = float4(0, EPSILON, 0, 0);
static const float4 Dz = float4(0, 0, EPSILON, 0);
static const float InvDx = 1.0 / (2.0 * EPSILON);
static const float4 Psi1 = float4(10, 0, 0, 0);
static const float4 Psi2 = float4(0, 10, 0, 0);
static const float4 Psi3 = float4(0, 0, 10, 0);

float Dt;
float Time;
float Speed;
float NoiseScale = 0.1;
StructuredBuffer<float3> PosIn;
RWStructuredBuffer<float3> PosOut;



float Pot(float4 xyzt) {
	return snoise(xyzt);
}
float3 Curl(float3 xyz) {
	xyz *= NoiseScale;
	float4 p1 = float4(xyz, Time) + Psi1;
	float4 p2 = float4(xyz, Time) + Psi2;
	float4 p3 = float4(xyz, Time) + Psi3;

	float dp1dy = (Pot(p1 + Dy) - Pot(p1 - Dy)) * InvDx;
	float dp1dz = (Pot(p1 + Dz) - Pot(p1 - Dz)) * InvDx;
	float dp2dz = (Pot(p2 + Dz) - Pot(p2 - Dz)) * InvDx;
	float dp2dx = (Pot(p2 + Dx) - Pot(p2 - Dx)) * InvDx;
	float dp3dx = (Pot(p3 + Dx) - Pot(p3 - Dx)) * InvDx;
	float dp3dy = (Pot(p3 + Dy) - Pot(p3 - Dy)) * InvDx;
	
	return float3(dp3dy - dp2dz, dp1dz - dp3dx, dp2dx - dp1dy);
}

[numthreads(WIDTH,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID) {
	float3 xyz = PosIn[id.x];
	xyz += Speed * Curl(xyz) * Dt;
    PosOut[id.x] = xyz;
}
